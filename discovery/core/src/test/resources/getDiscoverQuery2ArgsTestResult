PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#> 
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#> 
PREFIX sawsdl: <http://www.w3.org/ns/sawsdl#> 
PREFIX msm: <http://cms-wg.sti2.org/ns/minimal-service-model#> 
PREFIX wsdl: <http://www.w3.org/ns/wsdl-rdf#> 
PREFIX msee: <http://msee.sti2.at/properties#> 
PREFIX hr: <http://www.wsmo.org/ns/hrests#> 

# v1.0

CONSTRUCT {
	?serviceID rdf:type msm:Service .
	?serviceID msm:hasOperation ?operation .
	?serviceID sawsdl:modelReference ?category .
	
	?serviceID msee:name ?serviceName ; 
		msee:decription ?serviceDescription ; 
		msee:requirements ?serviceRequirements ; 
		msee:access_point ?serviceAccessPoint ; 
		msee:type ?serviceType ; 
		msee:icon ?serviceIcon . 
	
	?operation a msm:Operation ;
		msm:hasOutput ?outputMessageContent ;
		msm:hasInput ?inputMessageContent ;
		sawsdl:modelReference ?operationRef ;
		hr:hasMethod ?operationmethod ;
		hr:hasAddress ?operationaddress. 
	
	?outputMessageContent rdfs:label ?outputMessageContentLabel ;
		sawsdl:loweringSchemaMapping ?loweringSchemaOut ;
		sawsdl:liftingSchemaMapping ?liftingSchemaOut ;
		sawsdl:modelReference ?outputRef .

	?inputMessageContent rdfs:label ?inputMessageContentLabel ;
		sawsdl:loweringSchemaMapping ?loweringSchemaIn ;
		sawsdl:liftingSchemaMapping ?liftingSchemaIn ;
		sawsdl:modelReference ?inputRef .
		


} WHERE { 
	?serviceID rdf:type msm:Service . 
	{		?serviceID sawsdl:modelReference <http://msee.sti2.at/categories#REST_WEB_SERVICE> .} 
	?serviceID msm:hasOperation ?operation .
	?serviceID sawsdl:modelReference ?category .
	
	# operation
	?operation a msm:Operation .
	?operation msm:hasOutput ?outputMessageContent .
	?operation msm:hasInput ?inputMessageContent .
	OPTIONAL{
		?operation sawsdl:modelReference ?operationRef .
	}
	OPTIONAL{
		?operation hr:hasMethod ?operationmethod.
		?operation hr:hasAddress ?operationaddress.
	}
	
	OPTIONAL{
		# output
		?outputMessageContent a msm:MessageContent .
		OPTIONAL{
			?outputMessageContent rdfs:label ?outputMessageContentLabel .
		}
		OPTIONAL{
			?outputMessageContent sawsdl:loweringSchemaMapping ?loweringSchemaOut .
		}
		OPTIONAL{
			?outputMessageContent sawsdl:liftingSchemaMapping ?liftingSchemaOut .
		}
		OPTIONAL{
			?outputMessageContent sawsdl:modelReference ?outputRef .
		}
	}

	OPTIONAL{
		# input
		?inputMessageContent a msm:MessageContent .
		OPTIONAL{
			?inputMessageContent rdfs:label ?inputMessageContentLabel .
		}
		OPTIONAL{
			?inputMessageContent sawsdl:loweringSchemaMapping ?loweringSchemaIn .
		}
		OPTIONAL{
			?inputMessageContent sawsdl:liftingSchemaMapping ?liftingSchemaIn .
		}
		OPTIONAL{
			?inputMessageContent sawsdl:modelReference ?inputRef .
		}
	}
	
	
	OPTIONAL {
		?serviceID msee:name ?serviceName . 
		?serviceID msee:decription ?serviceDescription . 
		?serviceID msee:requirements ?serviceRequirements . 
		?serviceID msee:access_point ?serviceAccessPoint . 
		?serviceID msee:type ?serviceType . 
		?serviceID msee:icon ?serviceIcon . 
	} 

}
